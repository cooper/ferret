#!/usr/bin/perl
# Copyright (c) 2014 Mitchell Cooper

use warnings;
use strict;
use 5.010;

use File::Slurp  'slurp';
use Scalar::Util 'blessed';
use JSON::XS;

BEGIN {
    use FindBin;
    use lib 'lib';
    our $bin = $FindBin::Bin;
    use lib "$FindBin::Bin/lib";
}

use Ferret;
use Ferret::Lexer;
use Ferret::Perl;

sub inter  (@);
sub interf (@);

# parse arguments.
my ($args, $fh, @files) = '';
while ($_ = shift) {
    push @files, $_ and next unless /^-/;
    $args .= substr $_, 1;
}
my %args = map  { $_ => 1 } split //, $args;
@files   = grep { substr($_, -3) eq 'frt' } @files;

# show help.
help() if !@files || $args{h};

# handle each file.
do_file($_) foreach @files;

sub do_file {
    my $file = shift;
    say $file;

    # open the output file.
    if ($args{s}) { $fh = \*STDOUT }
    else          { open $fh, '>', "$file.pm" }

    # parse the input file.
    my $str = eval { slurp($file) } or die "$!\n";

    # tokenize.
    my @tok = Ferret::Lexer::Tokenizer::tokenize($str);
    die "Error: ${$tok[0]}\n"
        if blessed $tok[0] && $tok[0]->isa('F::Error');
    if ($args{t}) {
        inter "--- Tokens ---\n";
        show_tok(@tok);
    }

    # construct elements.
    my $doc = F::Document->new(name => $file);
    my $err = Ferret::Lexer::Constructor::construct($doc, @tok);
    if ($args{d}) {
        inter "--- DOM ---\n";
        show_dom($doc);
    }
    die "Error: $$err\n" if $err;

    # compile to Perl.
    my $compiled = Ferret::Perl::main($doc);
    if ($args{p}) {
        require Perl::Tidy;
        Perl::Tidy::perltidy(source => \$compiled, destination => \$compiled);
    }

    # write to file.
    print $fh $compiled;
    close $fh;

}

sub show_tok {
    state $json = JSON::XS->new->allow_nonref(1);
    foreach (@_) {
        my ($label, $value) = @$_;
        interf "%15s | %s\n",
            $label,
            defined $value ? $json->encode($value) : '';
    }
}

sub show_dom {
    state $indent = 0;
    my $el = shift;
    return if $el->type eq 'Unknown' && !$args{u};
    interf "%s %s\n", '    ' x $indent, $el->desc;
    return unless $el->is_node;
    $indent++;
        show_dom($_) foreach $el->children;
    $indent--;
}

sub inter (@) {
    my $str = join '', @_;
    print $fh "# $str";
    print $str;
}

sub interf (@) {
    my $str = shift;
    $str = sprintf $str, @_;
    print $fh "# $str";
    print $str;
}

sub help {
print "usage: $0 [-tduscp] file [file ...]
-t      show tokenization
-d      show lexer object tree (DOM)
-u      show unknown elements in DOM
-s      write to STDOUT instead of file
-c      write tokens and DOM as comments
-p      beautify code with perltidy
-h      show help
";
    exit 0;
}
